# ======================================================
#  Calc CMake 配置文件
# ======================================================

# ======================================================
#  头文件搜索
# ======================================================

# 获取没有 ../.. 相对路径标记的绝对路径
# xxx/dist/calc/cmake/CalcConfig.cmake
get_filename_component(CALC_CONFIG_PATH "${CMAKE_CURRENT_LIST_DIR}" REALPATH)
message("CALC_CONFIG_PATH:${CALC_CONFIG_PATH}")

# 获取 xxx/dist/calc 目录
# 1 xxx/dist/calc/cmake/CalcConfig.cmake => xxx/dist/calc/cmake
get_filename_component(Calc_INSTALL_PATH "${CALC_CONFIG_PATH}" PATH)
# 2 xxx/dist/calc/cmake => xxx/dist/calc
get_filename_component(Calc_INSTALL_PATH "${Calc_INSTALL_PATH}" PATH)


set(Calc_BUILD_TYPE "@Calc_BUILD_TYPE@")
# 搜索头文件, 添加至全局变量 Calc_INCLUDE_DIRS
# xxx/dist/calc/cmake/include
set(CALC_INCLUDE_COMPONENTS "${Calc_INSTALL_PATH}/include")
set(Calc_INCLUDE_DIRS "")
foreach (include_component ${CALC_INCLUDE_COMPONENTS})
    message("include_component:${include_component}")
    # 转换为绝对路径
    get_filename_component(include_component_real_path "${include_component}" REALPATH)

    if (NOT EXISTS "${include_component_real_path}")
        # 目录不存在,则报警告
        message(WARNING "Calc: Include directory doesn't exist: '${include_component}'. Calc installation may be broken. Skip...")
    else ()
        # 添加到全局属性中
        list(APPEND Calc_INCLUDE_DIRS "${include_component_real_path}")
    endif ()
endforeach ()
unset(include_component_real_path)


# ======================================================
# 库文件搜索
# ======================================================
# 包含导出配置的 *.cmake 文件
include(${CMAKE_CURRENT_LIST_DIR}/CalcModule.cmake)

# 添加至全局变量 Calc_LIBS
set(CALC_LIB_COMPONENTS calc)
foreach (lib_component ${CALC_LIB_COMPONENTS})
    message("list#APPEND Calc_LIBS: ${lib_component}")
    list(APPEND Calc_LIBS "${lib_component}")
endforeach ()

# ======================================================
# 搜寻的结果、状态
# ======================================================
include(FindPackageHandleStandardArgs)
message("Calc_INSTALL_PATH:${Calc_INSTALL_PATH}")
message("Calc_INCLUDE_DIRS:${Calc_INCLUDE_DIRS}")
message("Calc_LIBS:${Calc_LIBS}")
find_package_handle_standard_args(
        Calc
        # 注意这里要传入变量的名称 "Calc_INSTALL_PATH" 而不是变量的值,
        # 如 ${Calc_INSTALL_PATH} 或者 "path/to/include" 都是错误的
        # REQUIRED_VARS ${Calc_INSTALL_PATH} ${Calc_INCLUDE_DIRS} ${Calc_LIBS}

        REQUIRED_VARS Calc_INSTALL_PATH Calc_INCLUDE_DIRS Calc_LIBS
)
message("Calc_FOUND:${Calc_FOUND}")